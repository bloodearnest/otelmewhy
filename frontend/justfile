# Frontend Django project justfile
set dotenv-load := true

# Default recipe
_default:
    @just --list

# Run the frontend Django server with gunicorn (with OpenTelemetry)
run:
    uv sync
    uv run python -m gunicorn --config gunicorn.conf.py wsgi:application --bind localhost:8000


# Run system integration tests
test n="64":
    #!/usr/bin/env bash
    set -euo pipefail

    # Check if frontend is running on localhost:8000
    if ! curl -s http://localhost:8000 > /dev/null 2>&1; then
        echo "Error: Frontend server not running on localhost:8000"
        echo "Please start both servers first in another terminal with: just run"
        exit 1
    fi

    echo "Servers detected, running system tests..."
    uv run python test_system.py -n {{ n }}

# Run Django checks
check:
    uv run python manage.py check

# Format code with ruff
format:
    uv run ruff format .

# Lint code with ruff
lint:
    uv run ruff check .

# Install/update dependencies
sync *args="":
    uv sync {{ args }}

# Open Django shell
shell:
    uv run python manage.py shell

# Collect static files
collectstatic:
    uv run python manage.py collectstatic --noinput
